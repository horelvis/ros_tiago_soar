sp {groceries*propose*init
    (state <s> ^type state
                -^upload_done)
-->
    (<s> ^operator <o>)
    (<o> ^name upload)
}

sp {groceries*propose*idle
    (state <s> ^upload_done
                -^cmd)
-->
    (<s> ^operator <o>)
    (<o> ^name check_cmd)
}

sp {groceries*propose*search
    (state <s> ^cmd <c>
                ^obj <obj>
                -^searchstatus)
    (<c> ^name search
        ^obj_name <oname>)
    (<obj> ^name <oname>)
-->
    (<s> ^operator <o>)
    (<o> ^name search
            ^obj <obj>)
}

sp {groceries*propose*presearch
    (state <s> ^cmd.name search
                -^known
                -^newobj)
-->
    (<s> ^operator <o>)
    (<o> ^name presearch)
}

sp {groceries*propose*searchresult
    (state <s> ^cmd.name search
                ^io.input-link <io>)
    (<io> ^status <status>)
-->
    (<s> ^operator <o>
        ^searchstatus <status>)
    (<o> ^name searchresult)
}

sp {groceries*propose*presearchresult
    (state <s> ^cmd.name search
                ^io.input-link <io>)
    (<io> ^new <new>)
-->
    (<s> ^operator <o>
        ^newobj <new>)
    (<o> ^name presearchresult)
}

#########################################   APPLY   ############################

sp {groceries*apply*init
    (state <s> ^operator.name upload
                ^io.output-link <out>)
-->
    (<s> ^upload_done 1)
    (<out> ^action <a>)
    (<a> ^name init)
}

sp {groceries*apply*idle
    (state <s> ^operator.name check_cmd
                ^io.output-link <out>
                ^io.input-link <io>)
    (<io> ^cmd <c>)
-->
    (<s> ^cmd <c>)
    (<out> ^action <a>)
    (<a> ^name commandreceived)
}

sp {groceries*apply*search
    (state <s> ^operator <o>
                ^io.output-link <out>)
    (<o> ^name search
        ^obj <obj>)
    (<obj> ^name <n>)
-->
    (<out> ^action <a>)
    (<a> ^name search
         ^obj_name <n>)
}

sp {groceries*apply*presearch
    (state <s> ^operator.name presearch
                ^io.output-link <out>)
-->
    (<out> ^action <a>)
    (<a> ^name presearch)
}

sp {groceries*apply*searchresult*found
    (state <s> ^operator.name searchresult
                ^searchstatus 1
                ^cmd <c>
                ^io.output-link <out>)
-->
    (<s> ^known 1 -)
    (<s> ^cmd <c> -)
    (<s> ^upload_done 1 -)
    (<s> ^searchstatus 1 -)
    (<out> ^action <a>)
    (<a> ^name searchresultfound)
}

sp {groceries*apply*searchresult*notfound
    (state <s> ^operator.name searchresult
                ^searchstatus 0
                ^cmd <c>
                ^io.output-link <out>)
-->
    (<s> ^known 1 -)
    (<s> ^cmd <c> -)
    (<s> ^upload_done 1 -)
    (<s> ^searchstatus 0 -)
    (<out> ^action <a>)
    (<a> ^name searchresultnotfound)
}

sp {groceries*apply*presearchresult*found
    (state <s> ^operator.name presearchresult
                ^newobj 1
                ^io.output-link <out>
                ^io.input-link <io>)
    (<io> ^obj <obj>)
-->
    (<s> ^newobj 1 -
         ^obj <obj>)
    (<out> ^action <a>)
    (<a> ^name presearchresultfound)
}

sp {groceries*apply*presearchresult*notfound
    (state <s> ^operator.name presearchresult
                ^newobj 0
                ^cmd <c>
                ^io.output-link <out>)
-->
    (<s> ^cmd <c> -)
    (<s> ^upload_done 1 -)
    (<s> ^newobj 0 -)
    (<out> ^action <a>)
    (<a> ^name presearchresultnotfound)
}

############################   ELABORATES #########################
sp {groceries*elaborate*isknown
    (state <s> ^cmd <c>
                ^obj <obj>)
    (<c> ^obj_name <oname>)
    (<obj> ^name <oname>)
-->
    (<s> ^known 1)
}
